plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.dagger.hilt)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.jetbrains.kotlin.serialization)
    alias(libs.plugins.kapt)
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    alias(libs.plugins.realm.kotlin)
}

def javaVersion = JavaVersion.VERSION_17
def enableLeakCanary = false

android {

    namespace 'com.infomaniak.mail'

    compileSdk 34

    defaultConfig {
        applicationId 'com.infomaniak.mail'
        minSdk 25
        targetSdk 34
        versionCode 1_03_001_01
        versionName '1.3.1'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        setProperty "archivesBaseName", "infomaniak-mail-$versionName ($versionCode)"

        buildConfigField 'String', 'CLIENT_ID', '"E90BC22D-67A8-452C-BE93-28DA33588CA4"'
        buildConfigField 'String', 'CREATE_ACCOUNT_URL', '"https://welcome.infomaniak.com/signup/ikmail?app=true"'
        buildConfigField 'String', 'CREATE_ACCOUNT_SUCCESS_HOST', '"mail.infomaniak.com"'
        buildConfigField 'String', 'CREATE_ACCOUNT_CANCEL_HOST', '"welcome.infomaniak.com"'
        buildConfigField 'String', 'IMPORT_EMAILS_URL', '"https://import-email.infomaniak.com"'
        buildConfigField 'String', 'MAIL_API', '"https://mail.infomaniak.com"'
        // buildConfigField 'String', 'MAIL_API_PREPROD', '"https://mail.preprod.dev.infomaniak.ch"'
        buildConfigField 'String', 'SHOP_URL', '"https://ik.me"'
        buildConfigField 'String', 'CHATBOT_URL', '"https://www.infomaniak.com/chatbot"'
        buildConfigField 'String', 'MANAGE_SIGNATURES_URL', '"https://mail.infomaniak.com/0/settings/signatures"'

        buildConfigField 'String', 'BUGTRACKER_MAIL_BUCKET_ID', '"app_mail"'
        buildConfigField 'String', 'BUGTRACKER_MAIL_PROJECT_NAME', '"mail"'
        buildConfigField 'String', 'GITHUB_REPO', '"android-mail"'
        buildConfigField 'String', 'GITHUB_REPO_URL', '"https://github.com/Infomaniak/android-kMail"'

        resValue 'string', 'ATTACHMENTS_AUTHORITY', 'com.infomaniak.mail.attachments'

        resourceConfigurations += ["en", "de", "es", "fr", "it"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions { jvmTarget = javaVersion }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    flavorDimensions += 'distribution'

    productFlavors {
        standard {
            dimension 'distribution'
            apply plugin: 'com.google.gms.google-services'
            getIsDefault().set(true)
        }
        fdroid { dimension 'distribution' }
    }
}

dependencies {
    implementation project(path: ':Core')
    implementation project(path: ':Core:AppLock')
    implementation project(path: ':Core:BugTracker')
    implementation project(path: ':Core:Confetti')
    implementation project(path: ':Core:Stores')
    implementation project(path: ':HtmlCleaner')

    implementation libs.realm.kotlin.base

    standardImplementation libs.firebase.messaging.ktx

    implementation libs.lottie
    implementation libs.dragdropswipe.recyclerview
    implementation libs.dotsindicator

    implementation libs.flexbox
    implementation libs.lifecycle.process
    implementation libs.webkit
    implementation libs.work.concurrent.futures
    implementation libs.work.runtime.ktx

    implementation libs.hilt.android
    implementation libs.hilt.work
    // TODO: Migrate to KSP, actually Hilt is not yet supported with KSP
    // https://kotlinlang.org/docs/ksp-overview.html#supported-libraries
    // https://github.com/google/dagger/issues/2349
    kapt libs.hilt.compiler
    kapt libs.hilt.compiler.androidx

    implementation libs.sentry.android.fragment

    // Test
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit

    // Debug
    if (enableLeakCanary) debugImplementation libs.leakcanary.android
}
